# SPDX-FileCopyrightText: 2022-2023 Dennis Gl√§ser <dennis.glaeser@iws.uni-stuttgart.de>
# SPDX-License-Identifier: MIT

@PACKAGE_INIT@

set(@PROJECT_NAME@_LIBRARY @PROJECT_NAME@::@PROJECT_NAME@)
set_and_check(@PROJECT_NAME@_INCLUDE_DIRS "@PACKAGE_GRIDFORMAT_INCLUDE_DIRS@")
set_and_check(@PROJECT_NAME@_MODULE_DIR "@PACKAGE_GRIDFORMAT_INSTALL_CMAKE_DATA_DIR@/modules")
list(APPEND CMAKE_MODULE_PATH "@PACKAGE_GRIDFORMAT_INSTALL_CMAKE_DATA_DIR@/modules")

# find (optional) dependencies before including the targets
include(CMakeFindDependencyMacro)
if (@ZLIB_FOUND@)
    find_dependency(ZLIB)
endif ()
if (@LZ4_FOUND@)
    find_dependency(LZ4)
endif ()
if (@LZMA_FOUND@)
    find_dependency(LZMA)
endif()
if (@MPI_FOUND@)
    find_dependency(MPI)
endif ()
if (@GRIDFORMAT_HAVE_HIGH_FIVE@)
    # some variables from the configure step to match the behaviour when installed
    set(HIGHFIVE_USE_BOOST @Boost_FOUND@)
    set(HIGHFIVE_PARALLEL_HDF5 @HIGHFIVE_PARALLEL_HDF5@)
    if (GRIDFORMAT_NO_HIGHFIVE_HINTS)
        find_dependency(HighFive)
    else ()
        find_dependency(
            HighFive
            HINTS
                ${CMAKE_CURRENT_LIST_DIR}/../deps/HighFive  # for linking to the build tree
                @PACKAGE_GRIDFORMAT_INSTALL_CMAKE_DATA_DIR@/../HighFive  # in case HighFive was installed alongside gridformat
                @HighFive_ROOT@  # in case HighFive Root directory was given
        )
    endif ()
endif ()

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
check_required_components(@PROJECT_NAME@)

# make compile definitions for available dependencies accessible from cmake in downstream projects
get_target_property(_GFMT_IF_COMPILE_DEFS ${@PROJECT_NAME@_LIBRARY} INTERFACE_COMPILE_DEFINITIONS)
foreach (_GFMT_IF_COMPILE_DEF IN LISTS _GFMT_IF_COMPILE_DEFS)
    if (_GFMT_IF_COMPILE_DEF MATCHES "GRIDFORMAT_HAVE_*")
        set(${_GFMT_IF_COMPILE_DEF} true)
    endif ()
endforeach ()
